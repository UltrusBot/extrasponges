plugins {
    id "me.modmuss50.mod-publish-plugin" version "0.4.5"
}

evaluationDependsOn(":common")
evaluationDependsOn(":fabric")
evaluationDependsOn(":neoforge")

publishMods {
    changelog = rootProject.file("CHANGELOG.md").text
    displayName = "${mod_name} v${version}"
    type = STABLE

    def neoForgeLoaders = [
            "neoforge"
    ]
    def fabricLoaders = [
            "fabric",
            "quilt"
    ]
    def mcVersions = [
            "1.21"
    ]
    def fabricMcVersions = mcVersions
    def neoForgeMcVersions = mcVersions

    curseforge("curseforgeFabric") {
        file = project(":fabric").remapJar.archiveFile

        accessToken = providers.gradleProperty("CF_API_KEY")
        projectId = "457043"
        modLoaders.addAll(fabricLoaders)
        minecraftVersions.addAll(fabricMcVersions)

        requires {
            slug = "fabric-api"
        }
    }

    modrinth("modrinthFabric") {
        file = project(":fabric").remapJar.archiveFile

        accessToken = providers.gradleProperty("MODRINTH_TOKEN")
        projectId = "poGrqY8n"
        modLoaders.addAll(fabricLoaders)
        minecraftVersions.addAll(fabricMcVersions)

        requires {
            slug = "fabric-api"
        }
    }

    curseforge("curseforgeNeoforge") {
        file = project(":neoforge").jar.archiveFile

        accessToken = providers.gradleProperty("CF_API_KEY")
        projectId = "508940"
        modLoaders.addAll(neoForgeLoaders)
        minecraftVersions.addAll(neoForgeMcVersions)
    }

    modrinth("modrinthNeoforge") {
        file = project(":neoforge").jar.archiveFile

        accessToken = providers.gradleProperty("MODRINTH_TOKEN")
        projectId = "r7G43arb"
        modLoaders.addAll(neoForgeLoaders)
        minecraftVersions.addAll(neoForgeMcVersions)
    }

    github {
        additionalFiles.from(
                project(":common").jar.archiveFile,
                project(":common").sourcesJar.archiveFile,
                project(":fabric").remapJar.archiveFile,
                project(":fabric").remapSourcesJar.archiveFile,
                project(":neoforge").jar.archiveFile,
                project(":neoforge").sourcesJar.archiveFile)

        accessToken = providers.gradleProperty("GH_TOKEN")
        repository = "UltrusBot/ExtraSponges"
        commitish = "1.21" // This is the branch the release tag will be created from
        tagName = "v${version}"
    }
}