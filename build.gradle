plugins {
	alias libs.plugins.fabric.loom
	alias libs.plugins.publishing
	id 'maven-publish'
}
apply from: 'https://raw.githubusercontent.com/UltrusBot/GradleScripts/61556de3c834075f02353af9c9c55462c6f8bea0/release.gradle'
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

base {
	archivesName = project.archives_base_name
}
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name 'Quilt'
		url 'https://maven.quiltmc.org/repository/release'
	}
}

dependencies {
	minecraft(libs.minecraft)
	mappings(variantOf(libs.quilt.mappings) { classifier("intermediary-v2") })
	modImplementation(libs.fabric.loader)
	modImplementation(libs.fabric.api)}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}
publishMods {
	displayName = "Extra Sponges v${project.version}"
	file = remapJar.archiveFile
	changelog = file("$rootDir/CHANGELOG.md").text
	version = project.version
	type = STABLE
	modLoaders.addAll("fabric", "quilt")

	curseforge {
		projectId = "457043"
		minecraftVersions.addAll("1.20.3", "1.20.4")
		accessToken = providers.environmentVariable("CF_API_KEY")
	}
	modrinth {
		projectId = "poGrqY8n"
		minecraftVersions.addAll("1.20.3", "1.20.4")
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
	}
	github {
		repository = "UltrusBot/ExtraSponges"
		commitish = "1.20.4"
		accessToken = providers.environmentVariable("GH_TOKEN")
	}

	dryRun = false
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}